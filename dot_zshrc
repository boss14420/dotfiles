export GEMINI_API_KEY=$(cat ~/.config/gemini_api_key.txt)

################ FZF CONFIGURATION
export FZF_COMPLETION_TRIGGER='**'
export FZF_COMPLETION_OPTS='+c -x'

_fzf_compgen_path() {
  fd --hidden --follow --exclude ".git" . "$1"
}

_fzf_compgen_dir() {
  fd --type d --hidden --follow --exclude ".git" . "$1"
}

_fzf_comprun() {
  local command=$1
  shift

  case "$command" in
    cd)           fzf "$@" --preview 'tree -C {} | head -200' ;;
    export|unset) fzf "$@" --preview "eval 'echo \$'{}" ;;
    ssh)          fzf "$@" --preview 'dig {}' ;;
    *)            fzf "$@" ;;
  esac
}

################ ZINIT SETUP
### Added by Zinit's installer
if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load important annexes
zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

################ ZINIT PLUGINS
# Theme - bullet-train with prompt setup
setopt prompt_subst
zinit ice depth"1"
zinit load caiogondim/bullet-train.zsh

# Essential tools
zinit pack"bgn-binary+keys" for junegunn/fzf
zinit light "Aloxaf/fzf-tab"
zinit load "urbainvaes/fzf-marks"
zinit load "mfaerevaag/wd"

# zoxide
zinit ice wait"2" as"command" from"gh-r" lucid \
  mv"zoxide*/zoxide -> zoxide" \
  atclone"./zoxide init zsh > init.zsh" \
  atpull"%atclone" src"init.zsh" nocompile'!'
zinit light ajeetdsouza/zoxide

function zz() {
    __zoxide_zi "$@"
}

# Completions and suggestions
zinit load "zsh-users/zsh-completions"
zinit load "zsh-users/zsh-autosuggestions"

if [[ "$(tty)" != /dev/tty[0-9]* ]] && [[ "$SHELL" == *zsh ]]; then
    zinit as"program" atclone'./build.sh' \
        atpull'%atclone' pick"smart-suggestion" src"smart-suggestion.plugin.zsh" for \
            yetone/smart-suggestion
fi

# Syntax highlighting and history search (load last)
zinit load "zsh-users/zsh-syntax-highlighting"
zinit load "zsh-users/zsh-history-substring-search"

# OMZ plugins
zinit snippet OMZ::plugins/git/git.plugin.zsh
zinit snippet OMZ::plugins/docker/docker.plugin.zsh
zinit snippet OMZ::plugins/docker-compose/docker-compose.plugin.zsh
zinit snippet OMZ::plugins/cp/cp.plugin.zsh
zinit snippet OMZ::plugins/themes/themes.plugin.zsh
# zinit snippet OMZ::plugins/adb/adb.plugin.zsh

################ BASIC ZSH CONFIGURATION
# History configuration
HISTFILE=~/.histfile
HISTSIZE=10000
SAVEHIST=10000
setopt appendhistory autocd extendedglob notify
setopt HIST_FIND_NO_DUPS
setopt SHARE_HISTORY
setopt interactive_comments

# Key bindings
bindkey -v
# bindkey '^T' fzf-completion
unsetopt beep

# Completion setup
zstyle ':completion:*' completer _expand _complete _ignored _correct _approximate
zstyle ':completion:*' glob 'NUMERIC == 1'
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]}' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}'
zstyle ':completion:*' max-errors 1
zstyle ':completion:*' menu select

autoload -Uz compinit
compinit

# Autocompletions
autoload -U +X bashcompinit && bashcompinit
command -v vault >/dev/null && complete -o nospace -C /usr/bin/vault vault
command -v mcli >/dev/null && complete -o nospace -C /usr/bin/mcli mcli

################ THEME CONFIGURATION
ZSH_THEME="bullet-train"

function my_git_info() {
    local vcs_info=$(echo $vcs_info_msg_0_ | sed -re 's/.*\[(.*)\].*/\1/')
    echo "$ZSH_THEME_GIT_PROMPT_PREFIX$vcs_info$(parse_git_dirty)$ZSH_THEME_GIT_PROMPT_SUFFIX"
}

BULLETTRAIN_GIT_PROMPT_CMD="\$(my_git_info)"
BULLETTRAIN_EXEC_TIME_SHOW=true
BULLETTRAIN_KCTX_BG="gray"
BULLETTRAIN_VIRTUALENV_FG="black"

# Detect OS and customize bullet-train directory colors

# Load OS identification details if available
if [ -f /etc/os-release ]; then
    . /etc/os-release
    OS_ID=$ID
elif [ -f /etc/arch-release ]; then
    OS_ID="arch"
elif [ -f /etc/debian_version ]; then
    OS_ID="debian"
else
    # Fallback to check for common IDs if /etc/os-release exists but ID is not standard
    OS_ID="other"
fi

# Set default colors (Other: Gray BG / Black FG)
BULLETTRAIN_DIR_BG="242" # Light Gray (256 color code)
BULLETTRAIN_DIR_FG="0"   # Black

case "$OS_ID" in
    # Arch Linux: Blue BG / White FG
    arch)
        BULLETTRAIN_DIR_BG="12"  # Blue
        BULLETTRAIN_DIR_FG="15"  # White
        ;;

    # Debian Family (debian, ubuntu, mint, etc.): Green BG / White FG
    debian|ubuntu|mint|pop)
        BULLETTRAIN_DIR_BG="2"   # Green
        BULLETTRAIN_DIR_FG="15"  # White
        ;;

    # Fedora Family (fedora, rhel, centos, etc.): Yellow BG / Black FG
    fedora|rhel|centos|rocky|almalinux)
        BULLETTRAIN_DIR_BG="cyan"   # cyan
        BULLETTRAIN_DIR_FG="0"   # Black
        ;;
esac

# Note: These variables must be set before the bullet-train theme is sourced/loaded.

BULLETTRAIN_PROMPT_ORDER=(
  time
  status
  custom
  context
  #kctx
  dir
  screen
  virtualenv
  #aws
  # git
  cmd_exec_time
)

################ ENVIRONMENT VARIABLES
export EDITOR=vim
export GPG_TTY=$(tty)
export WORKON_HOME=~/.virtualenvs

# Colors for grep and ls
export GREP_COLOR="1;33"
eval $(dircolors -b)

# Less colors for man pages
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;44;33m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'
export LESS="-R"

# PATH additions
export PATH="${KREW_ROOT:-$HOME/.krew}/bin:$HOME/go/bin:$HOME/.dotnet/tools:$PATH"
export PATH=$PATH:$HOME/.cargo/bin

################ ALIASES
# Color and enhanced commands
command -v colordiff >/dev/null && alias diff='colordiff'
alias grep='grep --color=auto'
alias egrep='egrep --color=auto'
alias fgrep='fgrep --color=auto'
alias more='less'
alias df='df -h'
alias du='du -c -h'
alias mkdir='mkdir -p -v'
alias nano='nano -w'
alias ping='ping -c 5'
alias chmod="chmod -c"
alias chown="chown -c"
alias view='vim -v'
alias nautilus='nautilus --no-desktop'
alias nemo='nemo --no-desktop'

# Navigation shortcuts
alias ..='cd ..'
alias ...='cd ../..'
alias ....='cd ../../../'
alias .....='cd ../../../../'

# File operations
alias rmv='rsync -v --progress --remove-source-files'

# List commands
alias ls='ls -hF --color=auto'
alias lr='ls -R'
alias ll='ls -l'
alias la='ll -A'
alias lx='ll -BX'
alias lz='ll -rS'
alias lt='ll -rt'
alias lm='la | more'

# System information
alias da='date "+%A, %B %d, %Y [%T]"'
alias du1='du --max-depth=1'
alias hist='history | grep $1'
alias openports='netstat --all --numeric --programs --inet'
alias pg='ps -eo pid,tid,class,rtprio,ni,pri,psr,pcpu,stat,wchan:14,args | grep $1'
alias inxi='inxi -PACDGnm'

# Kubernetes shortcuts
alias kc='kubectl'
alias kn='kubens'
alias kx='kubectx'

# Docker shortcuts
alias dip="docker inspect -f '{{ range .NetworkSettings.Networks }}{{.IPAddress}}{{end}}'"

# System management
alias grub-update='grub-mkconfig -o /boot/grub/grub.cfg'
alias update-grub='grub-mkconfig -o /boot/grub/grub.cfg'
alias tmux="TERM=screen-256color-bce tmux"
alias xclip="xclip -selection c"

# Safety features
alias chown='chown --preserve-root'
alias chmod='chmod --preserve-root'
alias chgrp='chgrp --preserve-root'

# Privileged access (if not root)
if [ $UID -ne 0 ]; then
    alias sudo='sudo '
    alias scat='sudo cat'
    alias svim='sudo vim'
    alias root='sudo su'
    alias reboot='sudo reboot'
    alias halt='sudo halt'
    alias update='sudo pacman -Su'
    alias netcfg='sudo netcfg2'
fi

# assign OLD_TERM to tmux so it can detect nested session
alias tmux='OLD_TERM=$TERM tmux'

################ FUNCTIONS
function jwt-decode() {
  sed 's/\./\n/g' <<< $(cut -d. -f1,2 <<< $1) | base64 --decode | jq
}

################ EXTERNAL INTEGRATIONS
# Virtualenvwrapper
if [[ -f /usr/share/virtualenvwrapper/virtualenvwrapper_lazy.sh ]]; then
    source /usr/share/virtualenvwrapper/virtualenvwrapper_lazy.sh
elif [[ -f /usr/bin/virtualenvwrapper_lazy.sh ]]; then
    source /usr/bin/virtualenvwrapper_lazy.sh
fi

# Conda
[ -f /opt/miniconda3/etc/profile.d/conda.sh ] && source /opt/miniconda3/etc/profile.d/conda.sh

# # FZF
# [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# nvm
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion

# npm home binaries
command -v npm >/dev/null && export PATH="$(npm config get prefix)/bin:$PATH"

# nvim
export PATH=$HOME/.local/share/bob/nvim-bin:$PATH
export EDITOR=nvim

# prefer homebrew
export PATH=$HOME/.homebrew/bin:$PATH

################ TG NOTIFY FUNCTIONS
. $HOME/.config/telegram_bot.conf

# define hook to capture currently executing command
autoload -Uz add-zsh-hook

tg_report_preexec_hook() {
    # $1: The raw command string
    # $2: The expanded command
    # $3: The command as it will appear in history
    local s="$1"
    # Trim leading spaces
    export TG_REPORT_CURRENT_COMMAND="${${s##[[:space:]]#}%%[[:space:]]#}"
}

add-zsh-hook preexec tg_report_preexec_hook

_tg_send() {
    local token="$TELEGRAM_BOT_TOKEN"
    local chat_id="$TELEGRAM_BOT_CHAT_ID"
    [[ -z "$token" || -z "$chat_id" ]] && (
        echo "variable TELEGRAM_BOT_TOKEN or TELEGRAM_BOT_CHAT_ID not set"
        return 1
    )
    local text_payload="$1"
    [[ -z "$text_payload" ]] && return 1
    curl -s -X POST "https://api.telegram.org/bot$token/sendMessage" \
        -d "chat_id=$chat_id" \
        -d "text=$text_payload" \
        -d "parse_mode=Markdown" > /dev/null
}

_tg_payload_base() {
    local user=$(whoami)
    local host=$(hostname)
    local timestamp=$(date '+%Y-%m-%d %H:%M:%S.%3N')
    local full_payload="ðŸ–¥ *System Info*%0A*User:* $user%0A*Host:* $host%0A*Time:* $timestamp%0A%0A"
    echo "$full_payload"
}

tg_msg() {
    local message=""
    if [ ! -t 0 ]; then
        message=$(cat)
    else
        message="$1"
    fi
    [[ -z "$message" ]] && return 1
    local full_payload=$(_tg_payload_base)
    local full_payload="${full_payload}ðŸ’¬ *Message*%0A\`$message\`%0A%0A"
    _tg_send "$full_payload"
}

tg_last() {
    local exit_code=$?
    local last_cmd=$TG_REPORT_CURRENT_COMMAND
    if [[ "$last_cmd" == "tg_last" ]]; then
        last_cmd=$(fc -ln | sed -e 's/^[ \t]*//' -e 's/[ \t]*$//' | \
                   grep -vE '^tg_last' | grep -vE '^$' | tail -1)
    fi
    local user=$(whoami)
    local host=$(hostname)
    local timestamp=$(date '+%Y-%m-%d %H:%M:%S.%3N')
    local cwd=$(pwd)
    local cmd_status="SUCCESS âœ…"
    [[ $exit_code -ne 0 ]] && cmd_status="FAILED âŒ"

    local full_payload=$(_tg_payload_base)
    local full_payload="${full_payload}ðŸ“ *Directory*%0A\`$cwd\`%0A%0A"
    local message="${full_payload}ðŸ“Œ *Command*%0A\`$last_cmd\`%0A%0A*Status:* $cmd_status (Code: $exit_code)"
    _tg_send "$message"
}

tg_pipe() {
    local piped_cmd=$TG_REPORT_CURRENT_COMMAND
    local content=""
    if [ ! -t 0 ]; then
        content=$(cat)
        if (( ${#content} > 2048 )); then
            content=${content:0:2048}"%0A..."
        fi
    fi
    [[ -z "$content" ]] && return 1

    local cwd=$(pwd)

    local full_payload=$(_tg_payload_base)
    local full_payload="${full_payload}ðŸ“ *Directory*%0A\`$cwd\`%0A%0A"
    local full_payload="${full_payload}ðŸ“Œ *Piped Command*%0A\`$piped_cmd\`%0A%0A"
    local full_payload="${full_payload}ðŸ“¥ *Output*%0A\`$content\`%0A%0A"
    _tg_send "$full_payload"
}

################ TERMINAL SPECIFIC SETTINGS

# Fcitx for tty2
if [[ $(tty) = /dev/tty2 ]]; then
    fcitx-fbterm-helper -l
fi

################ HIGHLIGHTING STYLES
ZSH_HIGHLIGHT_STYLES[comment]='fg=grey'

################ STARTUP
command -v neofetch >/dev/null && neofetch
